// Inserts random numbers from 0 to 100 into a binary search tree until it 
// reaches size 50, then looks up the values for 1, 11, 21, ..., 91
fn main() {
  let tree = Bst {
    compare = compare_i32,
    root = leaf(10, 3.14),
  };

  while size(tree) < 50 {
    let key = random_int(0, 100);
    let value = random();
    set tree = insert(tree, key, value);
  };

  let i = 1;
  while i < 100 {
    match lookup(tree, i) {
      Option::None _ => { log_f32(0.0 - 1.0) },
      Option::Some found => { log_f32(found.value) },
    };
    set i = i + 10;
  }
}

import log : fn (i32) -> unit from log
import log_f32 : fn (f32) -> unit from log
import random : fn () -> f32 from random

variant Ordering {
  struct Lt {},
  struct Eq {},
  struct Gt {}
}

variant Option[a] {
  struct None {},
  struct Some { value: a },
}

fn unwrap_or[a](opt : Option[a], default : a) -> a {
  match opt {
    Option::None _ => { default },
    Option::Some x => { x.value },
  }
}

fn map_or[a, b](opt : Option[a], default : b, f : fn(a) -> b) -> b {
  match opt {
    Option::None _ => { default },
    Option::Some x => { f(x.value) },
  }
}

struct Bst[k, v] {
  compare: fn (k, k) -> Ordering,
  root: Option[Node[k, v]],
}

struct Node[k, v] {
  key: k,
  value: v,
  left: Option[Node[k, v]],
  right: Option[Node[k, v]],
}

fn leaf[k, v](key : k, value : v) -> Option[Node[k, v]] {
  Option::Some {
    value = Node {
      key = key,
      value = value,
      left = Option::None #[Node[k, v]] {},
      right = Option::None #[Node[k, v]] {},
    }
  }
}

fn insert_node[k, v](
  node : Node[k, v], 
  compare : fn(k, k) -> Ordering, 
  key : k, 
  value : v
) -> Node[k, v] {
  match compare(key, node.key) {
    Ordering::Eq _ => {
      Node {
        key = key,
        value = value,
        left = node.left,
        right = node.right,
      }
    },
    Ordering::Lt _ => {
      match node.left {
        Option::None _ => {
          Node {
            key = node.key,
            value = node.value,
            left = leaf(key, value),
            right = node.right,
          }
        },
        Option::Some left => {
          Node {
            key = node.key,
            value = node.value,
            left = Option::Some {
              value = insert_node(left.value, compare, key, value) 
            },
            right = node.right,
          }
        }
      }
    },
    Ordering::Gt _ => {
      match node.right {
        Option::None _ => {
          Node {
            key = key,
            value = value,
            left = node.left,
            right = leaf(key, value),
          }
        },
        Option::Some right => {
          Node {
            key = node.key,
            value = node.value,
            left = node.left,
            right = Option::Some {
              value = insert_node(right.value, compare, key, value) 
            },
          }
        }
      }
    }
  }
}

fn insert[k, v](tree : Bst[k, v], key : k, value : v) -> Bst[k, v] {
  match tree.root {
    Option::None _ => {
      Bst {
        compare = tree.compare,
        root = leaf(key, value),
      }
    },
    Option::Some root => {
      Bst {
        compare = tree.compare,
        root = Option::Some {
          value = insert_node(root.value, tree.compare, key, value),
        },
      }
    }
  }
}

fn lookup[k, v](tree : Bst[k, v], key : k) -> Option[v] {
  let current = tree.root;
  let result = Option::None #[v] {};
  let keep_going = true;
  while keep_going {
    match current {
      Option::None _ => {
        set keep_going = false;
      },
      Option::Some node => {
        match tree.compare(key, node.value.key) {
          Ordering::Lt _ => {
            set current = node.value.left;
          },
          Ordering::Gt _ => {
            set current = node.value.right;
          },
          Ordering::Eq _ => {
            set result = Option::Some { value = node.value.value };
            set keep_going = false;
          },
        }
      }
    }
  };
  result
}

fn compare_i32(a : i32, b : i32) -> Ordering {
  if a < b {
    Ordering::Lt {}
  } else { 
    if a > b {
      Ordering::Gt {}
    } else {
      Ordering::Eq {}
    }
  }
}

fn size[k, v](tree : Bst[k, v]) -> i32 {
  match tree.root {
    Option::None _ => { 0 },
    Option::Some root => { size_node(root.value) },
  }
}

fn size_node[k, v](node : Node[k, v]) -> i32 {
  let left = match node.left {
    Option::None _ => { 0 },
    Option::Some n => { size_node(n.value) },
  };
  let right = match node.right {
    Option::None _ => { 0 },
    Option::Some n => { size_node(n.value) },
  };
  1 + left + right
}

fn round(x : f32) -> i32 {
  let y = x + 0.5;
  i32_trunc_f32_s(y)
}

fn random_int(low : i32, high : i32) -> i32 {
  let scaled = random() * f32_convert_i32_s(high - low);
  low + round(scaled)
}
