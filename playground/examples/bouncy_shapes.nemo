// This function gets called using `requestAnimationFrame`, which tries to maintain
// a smooth framerate.
fn tick(elapsed_time_ms : F32) {
  clear();

  let idx = 0;
  while idx < array_len#[Shape](shapes) {
    collide_shape(shapes[idx]);
    tick_shape(shapes[idx], elapsed_time_ms);
    draw_shape(shapes[idx]);

    set idx = idx + 1
  };

  set idx = 0;
  while idx < array_len#[Particle](particles.buffer) {
    draw_particle(particles.buffer[idx]);
    let particle = particles.buffer[idx];
    set particle.time = f32_max(0.0, particle.time - elapsed_time_ms);

    set idx = idx + 1
  }
}

import move_to : fn (F32, F32) -> Unit from move_to
import line_to : fn (F32, F32) -> Unit from line_to
import arc : fn (F32, F32, F32, F32, F32) -> Unit from arc
import set_stroke_color : fn (F32, F32, F32) -> Unit from set_stroke_color
import begin_path : fn () -> Unit from begin_path
import close_path : fn () -> Unit from close_path
import stroke : fn () -> Unit from stroke
import clear : fn () -> Unit from clear_canvas

struct Color {
  r : F32,
  g : F32,
  b : F32
}

variant Body {
  struct Cube {
    size : F32
  },
  struct Circle {
    radius : F32
  }
}

struct Shape {
  x : F32,
  y : F32,
  vx : F32,
  vy : F32,
  body : Body,
  color : Color
}

struct Particle {
  x : F32,
  y : F32,
  color : Color,
  time : F32
}

struct RingBuffer {
  buffer : Array[Particle],
  watermark : I32
}

global two_pi = 3.14159265359 * 2.0
global particle_timer = 200.0

global red = Color { r = 180.0, g = 0.0, b = 0.0 }
global green = Color { r = 0.0, g = 180.0, b = 0.0 }
global blue = Color { r = 0.0, g = 0.0, b = 180.0 }

global shapes = [
  Shape { x =   0.0, y = 0.0, vx =  9.0, vy = 14.0, body = Body::Cube { size = 10.0 }, color = red },
  Shape { x = 100.0, y = 0.0, vx = 10.0, vy = 13.0, body = Body::Cube { size = 15.0 }, color = green },
  Shape { x = 200.0, y = 0.0, vx = 11.0, vy = 12.0, body = Body::Circle { radius = 10.0 }, color = blue },
  Shape { x = 300.0, y = 0.0, vx = 12.0, vy = 11.0, body = Body::Cube { size = 25.0 }, color = blue },
  Shape { x = 400.0, y = 0.0, vx = 13.0, vy = 10.0, body = Body::Cube { size = 30.0 }, color = green },
  Shape { x = 500.0, y = 0.0, vx = 14.0, vy =  9.0, body = Body::Circle { radius = 17.0 }, color = red },
  Shape { x = 500.0, y = 0.0, vx = 19.0, vy = 17.0, body = Body::Cube { size = 35.0 }, color = blue }
]

global particles = RingBuffer {
  buffer = array_new#[Particle](Particle { x = 0.0, y = 0.0, color = red, time = 0.0 }, 25),
  watermark = 0
}

fn add_particle(p : Particle) {
  set particles.buffer[particles.watermark] = p;
  set particles.watermark =
    i32_rem_s(particles.watermark + 1, array_len#[Particle](particles.buffer))
}

fn clamp(min : F32, val : F32, max : F32) -> F32 {
  f32_min(max, f32_max(min, val))
}

fn size(body : Body) -> F32 {
  match body {
    Body::Cube c => { c.size },
    Body::Circle c => { c.radius * 2.0 }
  }
}

fn draw_body(x : F32, y : F32, body : Body) {
  match body {
    Body::Cube c => {
      move_to(x, y);
      line_to(x , y + c.size);
      line_to(x + c.size, y + c.size);
      line_to(x + c.size, y);
    },
    Body::Circle c => {
      let half_radius = c.radius / 2.0;
      arc(x + half_radius, y + half_radius, c.radius, 0.0, two_pi);
    }
  }
}

fn draw_shape(shape : Shape) {
  begin_path();
  draw_body(shape.x, shape.y, shape.body);
  set_stroke_color(shape.color.r, shape.color.g, shape.color.b);
  close_path();
  stroke()
}

fn draw_particle(particle : Particle) {
  if particle.time > 0.0 {
    begin_path();
    arc(particle.x, particle.y, (particle_timer - particle.time) / 2.0, 0.0, two_pi);
    set_stroke_color(particle.color.r, particle.color.g, particle.color.b);
    stroke()
  } else {}
}

fn collide_shapes(c1 : Shape, c2 : Shape) {
  let overlap_x = c1.x < c2.x + size(c2.body) && c1.x + size(c1.body) > c2.x;
  let overlap_y = c1.y < c2.y + size(c2.body) && c1.y + size(c1.body) > c2.y;

  if overlap_y && overlap_x {
    add_particle(Particle { x = c1.x, y = c1.y, color = c1.color, time = particle_timer });

    let delta_x = c1.x - c2.x;
    let delta_y = c1.y - c2.y;
    if f32_abs(delta_x) > f32_abs(delta_y) {
      set c1.vx = f32_copysign(c1.vx, delta_x)
    } else {
      set c1.vy = f32_copysign(c1.vy, delta_y)
    }
  } else {}
}

fn collide_shape(shape : Shape) {
  let idx = 0;
  while idx < array_len#[Shape](shapes) {
    if shapes[idx].x == shape.x && shapes[idx].y == shape.y {
    } else {
      collide_shapes(shape, shapes[idx])
    };
    set idx = idx + 1
  }
}

fn tick_shape(shape : Shape, elapsed_time_ms : F32) {
  let elapsed_factor = elapsed_time_ms / 48.0;

  set shape.x = shape.x + shape.vx * elapsed_factor;
  set shape.y = shape.y + shape.vy * elapsed_factor;

  if shape.x < 0.0 || shape.x > 500.0 {
    set shape.x = clamp(0.0, shape.x, 500.0);
    set shape.vx = f32_neg(shape.vx)
  } else {};

  if shape.y < 0.0 || shape.y > 500.0 {
    set shape.y = clamp(0.0, shape.y, 500.0);
    set shape.vy = f32_neg(shape.vy)
  } else {}
}
