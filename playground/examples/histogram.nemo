global frame_timer = 0.0

fn tick(time_elapsed_ms: f32) -> unit {
  set frame_timer = frame_timer + time_elapsed_ms;
  if frame_timer > 100.0 {
    set frame_timer = 0.0;
    clear();
    let generator = fn() -> i32 { random_int(0, 99) };
    let buckets = histogram(10, generator);
    render_canvas(buckets)
  } else {}
}

fn main() {
  let generator = fn() -> i32 { random_int(0, 99) };
  let buckets = histogram(10, generator);
  render(buckets)
}

fn histogram(count : i32, generator : fn () -> i32) -> [i32] {
  let buckets = [0, 0, 0, 0, 0];
  while count > 0 {
    let bucket = generator() / 20;
    set buckets[bucket] = buckets[bucket] + 1;
    set count = count - 1
  };
  buckets
}

import print_char : fn (i32) -> unit from print_char

global star = bytes_get("*", 0)
global line_feed = 10

fn render(buckets : [i32]) -> unit {
  let i = 0 ;
  while i < array_len#[i32](buckets) {
    print_stars(buckets[i]);
    print_char(line_feed);
    set i = i + 1
  }
}

fn print_stars(n : i32) {
  let i = 0 ;
  while i < n {
    print_char(star);
    set i = i + 1
  }
}

import random : fn () -> f32 from random

fn round(x : f32) -> i32 {
  let y = x + 0.5;
  i32_trunc_f32_s(y)
}

fn random_int(low : i32, high : i32) -> i32 {
  let scaled = random() * f32_convert_i32_s(high - low);
  low + round(scaled)
}
import move_to : fn (f32, f32) -> unit from move_to
import line_to : fn (f32, f32) -> unit from line_to
import arc : fn (f32, f32, f32, f32, f32) -> unit from arc
import set_stroke_color : fn (f32, f32, f32) -> unit from set_stroke_color
import begin_path : fn () -> unit from begin_path
import close_path : fn () -> unit from close_path
import stroke : fn () -> unit from stroke
import clear : fn () -> unit from clear_canvas

fn render_canvas(buckets: [i32]) {
  let i = 0;
  while i < array_len#[i32](buckets) {
    let x = f32_convert_i32_s(i * 100);
    let y = f32_convert_i32_s(buckets[i] * 50);
    draw_rect(x, 500.0 - y, 100.0, y);
    set i = i + 1
  }
}

struct Color {
  r : f32,
  g : f32,
  b : f32
}

fn random_color() -> Color {
  Color { r = random() * 255.0, g = random() * 255.0, b = random() * 255.0 }
}

fn draw_rect(x: f32, y: f32, width: f32, height: f32) {
  let color = random_color();
  begin_path();
  move_to(x, y);
  line_to(x + width, y);
  line_to(x + width, y + height);
  line_to(x, y + height);
  close_path();
  set_stroke_color(color.r, color.g, color.b);
  stroke();
}
