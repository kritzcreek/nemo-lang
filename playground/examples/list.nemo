// Shows how to use a variant type to implement a simple linked list
// and then use it to implement a few polymorphic higher order functions.
// Both via recursion and imperative style.
fn main() {
  let list = range(0, 10);
  let squared = map#[i32, i32](\(x) { x * x }, list);
  for_each#[i32](log, squared);
  log(sum(squared))
}

import log : fn (i32) -> unit from log

variant List[a] {
  struct Nil {},
  struct Cons {
    head : a,
    tail : List[a]
  }
}

fn map[a, b](f : fn (a) -> b, as : List[a]) -> List[b] {
  match as {
    List::Nil l => { List::Nil {} },
    List::Cons l => {
      let new_head = f(l.head);
      List::Cons {
        head = new_head,
        tail = map#[a, b](f, l.tail)
      }
    }
  }
}

fn for_each[a](f : fn (a) -> unit, as : List[a]) {
  while true {
    match as {
      List::Nil l => { return {} },
      List::Cons l => {
        f(l.head);
        set as = l.tail
      }
    }
  }
}

fn range(lo : i32, hi : i32) -> List[i32] {
  let result : List[i32] = List::Nil {};
  while lo < hi {
    set result = List::Cons { head = hi, tail = result };
    set hi = hi - 1
  };
  result
}

fn sum(xs : List[i32]) -> i32 {
  let result = 0;
  match xs {
    List::Nil _ => { 0 },
    List::Cons l => { l.head + sum(l.tail) }
  }
}
