module hamt
exports (Hamt, new, get, add, remove)

use option

struct Hamt[a] {
  root : Node::Mapped[a],
  size : I32,
}

variant Node[a] {
  struct Mapped { bitmap : I32, nodes : Array[Node[a]] },
  struct Leaf { hash : I32, value : a },
}

fn new[a]() -> Hamt[a] {
  Hamt { root = Node::Mapped { bitmap = 0, nodes = [] }, size = 0 }
}

fn get[a](hamt : Hamt[a], hash : I32) -> option::Option[a] {
  match get_with_anchor(hamt.root, 0, hash).payload {
    GetResultPayload::Success s => {
      option::Option::Some { val = s.node.value }
    },
    GetResultPayload::Missing _ => { option::Option::None {} },
    GetResultPayload::Conflict _ => { option::Option::None {} },
  };
}

fn add[a](hamt : Hamt[a], hash : I32, value : a) -> option::Option[a] {
  let get_result = get_with_anchor(hamt.root, 0, hash);
  match get_result.payload {
    GetResultPayload::Missing _ => {
      set hamt.size = hamt.size + 1;
      let n = get_result.anchor;
      let pos = bitpos(hash, get_result.shift);
      set n.bitmap = i32_or(n.bitmap, pos);
      let ix = index(n.bitmap, pos);
      set n.nodes = insert_node(n.nodes, ix, Node::Leaf { hash = hash, value = value });
      option::Option::None {};
    },
    GetResultPayload::Conflict c => {
      set hamt.size = hamt.size + 1;
      let n = get_result.anchor;
      let pos = bitpos(hash, get_result.shift);
      let ix = index(n.bitmap, pos);
      let new_node = merge_leafs(get_result.shift + 5, c.node, hash, value);
      set n.nodes[ix] = new_node;
      option::Option::None {};
    },
    GetResultPayload::Success s => {
      let prev = s.node.value;
      set s.node.value = value;
      option::Option::Some { val = prev };
    },
  };
}

fn remove[a](hamt : Hamt[a], hash : I32) -> option::Option[a] {
  match remove_rec(hamt.root, 0, hash) {
    RemoveResult::Success s => {
      set hamt.size = hamt.size - 1;
      option::Option::Some { val = s.node.value }
    },
    RemoveResult::Gathered g => {
      set hamt.size = hamt.size - 1;
      option::Option::Some { val = g.removed.value }
    },
    RemoveResult::NotFound n => { option::Option::None {} },
  };
}

fn mask(hash : I32, shift : I32) -> I32 {
  i32_shr_u(hash, i32_and(shift, 31))
}

fn bitpos(hash : I32, shift : I32) -> I32 {
   i32_shl(1, mask(hash, shift))
}

fn index(bitmap : I32, pos : I32) -> I32 {
  i32_popcnt(i32_and(bitmap, (pos - 1)))
}

fn merge_leafs[a](shift : I32, left : Node::Leaf[a], hash : I32, value : a) -> Node[a] {
  let next_pos1 = bitpos(left.hash, shift);
  let next_pos2 = bitpos(hash, shift);
  if next_pos1 != next_pos2 {
    let bitmap = i32_or(next_pos1, next_pos2);
    let right = Node::Leaf { hash = hash, value = value };
    let nodes : Array[Node[a]] = if next_pos1 < next_pos2 {
      [left, right]
    } else {
      [right, left]
    };
    Node::Mapped { bitmap = bitmap, nodes = nodes };
  } else {
    let new_node = merge_leafs(shift + 5, left, hash, value);
    Node::Mapped { bitmap = next_pos1, nodes = [new_node] };
  };
}

variant RemoveResult[a] {
  struct Success {
    node : Node::Leaf[a],
  },
  struct NotFound {},
  struct Gathered {
    new_node : Node::Leaf[a],
    removed : Node::Leaf[a],
  },
}

fn remove_rec[a](node : Node::Mapped[a], shift : I32, hash : I32) -> RemoveResult[a] {
  let pos = bitpos(hash, shift);
  if i32_and(pos, node.bitmap) == 0 {
    return RemoveResult::NotFound {};
  } else {};
  let ix = index(node.bitmap, pos);
  let next_node = node.nodes[ix];
  match next_node {
    Node::Mapped n => {
      let res = remove_rec(n, shift + 5, hash);
      match res {
        RemoveResult::Gathered gathered => {
          if shift != 0 && i32_popcnt(n.bitmap) == 1 {
            res;
          } else {
            set n.nodes[ix] = gathered.new_node;
            RemoveResult::Success { node = gathered.removed };
          };
        },
        _ => { res },
      };
    },
    Node::Leaf l => {
      if hash != l.hash {
        RemoveResult::NotFound {};
      } else {
        let rows = i32_popcnt(node.bitmap);
        // We never gather the root node
        if rows == 1 && shift == 0 {
          set node.bitmap = 0;
          set node.nodes = [];
          return RemoveResult::Success { node = l };
        } else {};
        if rows == 2 {
          let other = if ix == 0 { node.nodes[1] } else { node.nodes[0] };
          match other {
            Node::Leaf other_l => {
              if shift != 0 {
                return RemoveResult::Gathered { new_node = other_l, removed = l };
              } else {};
            },
            _ => {},
          };
          set node.bitmap = i32_and(node.bitmap, i32_xor(pos, 0 - 1));
          set node.nodes = [other];
          RemoveResult::Success { node = l };
        } else {
          let new_nodes = remove_node(node.nodes, ix);
          set node.bitmap = i32_and(node.bitmap, i32_xor(pos, 0 - 1));
          set node.nodes = new_nodes;
          RemoveResult::Success { node = l };
        };
      };
    },
  };
}

fn remove_node[a](as : Array[a], ix : I32) -> Array[a] {
  let old_len = array_len#[a](as);
  if old_len == 1 {
    [];
  } else {
    let res = array_new#[a](as[0], old_len - 1);
    array_copy#[a](res, 0, as, 0, ix);
    array_copy#[a](res, ix, as, ix + 1, old_len - ix - 1);
    res;
  };
}

fn insert_node[a](as : Array[a], ix : I32, elem : a) -> Array[a] {
  let old_len = array_len#[a](as);
  if old_len == 0 {
    [elem];
  } else {
    let res = array_new#[a](elem, old_len + 1);
    array_copy#[a](res, 0, as, 0, ix);
    array_copy#[a](res, ix + 1, as, ix, old_len - ix);
    res;
  };
}

struct GetResult[a] {
  shift : I32,
  anchor : Node::Mapped[a],
  payload : GetResultPayload[a],
}

variant GetResultPayload[a] {
  struct Success { node : Node::Leaf[a] },
  struct Conflict { node : Node::Leaf[a] },
  struct Missing {},
}

fn get_with_anchor[a](
  anchor : Node::Mapped[a],
  shift : I32,
  hash : I32,
) -> GetResult[a] {
  let pos = bitpos(hash, shift);
  if i32_and(anchor.bitmap, pos) == 0 {
    GetResult {
      shift = shift,
      anchor = anchor,
      payload = GetResultPayload::Missing {},
    };
  } else {
    let ix = index(anchor.bitmap, pos);
    let n = anchor.nodes[ix];
    match n {
      Node::Mapped anchor => {
        get_with_anchor(anchor, shift + 5, hash);
      },
      Node::Leaf leaf => {
        if leaf.hash == hash {
          GetResult {
            shift = shift,
            anchor = anchor,
            payload = GetResultPayload::Success { node = leaf },
          };
        } else {
          GetResult {
            shift = shift,
            anchor = anchor,
            payload = GetResultPayload::Conflict { node = leaf },
          };
        };
      },
    };
  };
}
