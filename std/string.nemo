module string
exports (Str, unsafe_new, concat, eq, chars, codepoint_len)

use prim
use option

// TODO: We should be able to hide the implementation details of Str
struct Str {
  buf : Bytes,
  offset : I32,
  len : I32,
}

fn concat(a : Str, b : Str) -> Str {
  let len = a.len + b.len;
  let buf = prim::bytes_new(0, len);
  prim::bytes_copy(buf, 0, a.buf, a.offset, a.len);
  prim::bytes_copy(buf, a.len, b.buf, b.offset, b.len);
  Str { buf = buf, offset = 0, len = len }
}

fn eq(a : Str, b : Str) -> Bool {
  when a.len != b.len {
    return false;
  };
  let i = 0;
  while i < a.len {
    when prim::bytes_get(a.buf, a.offset + i) != prim::bytes_get(b.buf, b.offset + i) {
      return false;
    };
    set i = i + 1;
  };
  true
}

// Assumes well formed utf8
fn unsafe_new(buf : Bytes) -> Str {
  Str { buf = buf, offset = 0, len = prim::bytes_len(buf) }
}

fn chars(s : Str) -> fn () -> option::Option[I32] {
  let offset = MutBox { value = s.offset };
  \() {
    if offset.value >= s.offset + s.len {
      option::Option::None {}
    } else {
      let result = codepoint_at_byte(offset.value, s.buf);
      set offset.value = offset.value + result.byte_count;
      option::Option::Some { val = result.codepoint }
    }
  }
}

struct CodepointAtByte {
  codepoint : I32,
  byte_count : I32
}

struct MutBox[a] {
  value : a
}

// Assumes well formed utf8
fn codepoint_at_byte(offset : I32, bs : Bytes) -> CodepointAtByte {
  let first_byte = prim::bytes_get(bs, offset);
  // ASCII case
  if prim::i32_and(first_byte, 0x80) == 0 {
    CodepointAtByte { codepoint = first_byte, byte_count = 1 }
  } else {
    let extra_bytes = prim::i32_xor(first_byte, 0 - 1);
    if extra_bytes == 1 {
      let res = (first_byte & 63) << 6;
      set res = res + (prim::bytes_get(bs, offset + 1) & 63);
      CodepointAtByte { codepoint = res, byte_count = 2 }
    } else {
      if extra_bytes == 2 {
        let res = (first_byte & 15) << 6;
        set res = (res + prim::bytes_get(bs, offset + 1) & 63) << 6;
        set res = res + (prim::bytes_get(bs, offset + 2) & 63);
        CodepointAtByte { codepoint = res, byte_count = 3 }
      } else {
        let res = (first_byte & 7) << 6;
        set res = (res + (prim::bytes_get(bs, offset + 1) & 63)) << 6;
        set res = (res + (prim::bytes_get(bs, offset + 2) & 63)) << 6;
        set res = res + (prim::bytes_get(bs, offset + 3) & 63);
        CodepointAtByte { codepoint = res, byte_count = 4 }
      }
    }
  }
}

fn codepoint_len(str : Str) -> I32 {
  let result = 0;
  let i = str.offset;
  let bytes_len = str.len;
  while i < bytes_len {
    let b = prim::bytes_get(str.buf, i);
    when b & 0xC0 != 0x80 {
      set result = result + 1;
    };
    set i = i + 1;
  };
  result
}
