Root = Module

Module = ModHeader? ModUse* TopLevel*

ModHeader = 'ident' ModExport*

ModExport = ModExportVal | ModExportTy | ModExportAll
ModExportVal = 'ident'
ModExportTy = 'upper_ident'
ModExportAll = 'dotdotdot'

ModUse = 'ident'

// TopLevel
TopLevel
  = TopImport
  | TopGlobal
  | TopStruct
  | TopVariant
  | TopFn

TopImport = 'import' ImpInternal ':' Type 'from' ImpExternal
ImpInternal = 'ident'
ImpExternal = 'ident'

TopGlobal = 'global' 'ident' ':' Type '=' Expr

TopStruct = 'struct' 'upper_ident' type_params:ParamTy* '{' StructField* '}'
StructField = 'ident' ':' Type

TopVariant = 'variant' 'upper_ident' type_params:ParamTy* '{' TopStruct* '}'

TopFn = 'fn' 'ident' ParamTy* ParamList ('->' Type)? body:EBlock
ParamList = Param*
Param = 'ident' ':' Type
ParamTy = 'ident'

// Types
Type
  = TyVar
  | TyCons
  | TyTuple
  | TyFn

TyCons = ModQualifier? Qualifier? 'upper_ident' type_args:Type*
TyTuple = Type*
TyVar = 'ident'

ModQualifier = 'ident' '::'
Qualifier = 'upper_ident' '::'

TyFn = 'fn' TyArgList '->' result:Type
TyArgList = '(' Type* ')'

// Literals
Literal = LitBool | LitFloat | LitInt | LitBytes

LitInt = 'int_lit' | 'binary_lit' | 'hex_lit'
LitBool = 'true' | 'false'
LitFloat = 'float_lit'
LitBytes = 'bytes_lit'

// Expressions
Expr
  = ELit
  | EVar
  | EArray
  | EStruct
  | ECall
  | EParen
  | ETuple
  | ETupleIdx
  | EUnary
  | EBinary
  | EArrayIdx
  | EStructIdx
  | EIf
  | EWhen
  | EMatch
  | ELambda
  | EBlock
  | EReturn

ELit = Literal
EVar = ModQualifier? 'ident'
EArray = '[' Expr* ']'
EParen = '(' Expr ')'
ETuple = '(' Expr* ')'
ETupleIdx = Expr 'int_lit'
EArrayIdx = Expr '[' index:Expr ']'

ECall = Expr ETyArgList? EArgList
EArgList = '(' Expr* ')'
ETyArgList = Type*

EUnary = op:('+' | '-') Expr
EBinary = lhs:Expr op:('+' | '-') rhs:Expr

EStruct = ModQualifier? Qualifier? 'upper_ident' ETyArgList? '{' EStructField* '}'
EStructField = 'ident' '=' Expr

EStructIdx = Expr '.' 'ident'
EIf = Condition then_branch:EBlock else_branch:EBlock
EWhen = Condition branch:Expr
Condition = Expr

EMatch = 'match' scrutinee:Expr '{' EMatchBranch* '}'
EMatchBranch = Pattern '=>' body:EBlock

ELambda = ParamList return_ty:Type body:Expr

EBlock = '{' Declaration* '}'
EReturn = 'return' Expr

// Declarations
Declaration
  = DLet
  | DSet
  | DWhile
  | DExpr

DLet = 'let' 'ident' (':' Type)? '=' Expr
DSet = 'set' SetTarget '=' Expr
DWhile = 'while' Expr EBlock
DExpr = Expr

SetTarget = SetTargetExpr
SetTargetExpr = EVar | EArrayIdx | EStructIdx

Pattern
  = PatVariant
  | PatVar

PatVariant = ModQualifier? Qualifier 'upper_ident' 'ident'
PatVar = 'ident'
